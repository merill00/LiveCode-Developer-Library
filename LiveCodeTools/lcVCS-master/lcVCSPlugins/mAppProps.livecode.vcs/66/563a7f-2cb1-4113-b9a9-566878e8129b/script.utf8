/**
 *  This file is part of lcVCS (C) 2013 M E R Goulding.
 *  
 *  lcVCS is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *  
 *  lcVCS is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *  
 *  You should have received a copy of the GNU General Public License
 *  along with lcVCS.  If not, see <http://www.gnu.org/licenses/>.
 */

on lcVCSFilterExportedProps pObject,@pProps
   -- translate row template to UUID for export
   repeat for each key tRuleIndex in pProps["layout"]
      repeat for each key tObjectIndex in pProps["layout"][tRuleIndex]["objects"]
         if exists (control id pProps["layout"][tRuleIndex]["objects"][tObjectIndex]["id"] of pObject) then
            put UUIDForObject(the long id of control id pProps["layout"][tRuleIndex]["objects"][tObjectIndex]["id"] of pObject) into \
                  pProps["layout"][tRuleIndex]["objects"][tObjectIndex]["id"]
         end if
      end repeat
      repeat for each item tSide in "left,top,right,bottom"
         if pProps["layout"][tRuleIndex][tSide]["relative object"] is not empty and \
               pProps["layout"][tRuleIndex][tSide]["relative object"] is not "card" then
            if exists (control id pProps["layout"][tRuleIndex][tSide]["relative object"] of pObject) then
               put UUIDForObject(the long id of control id pProps["layout"][tRuleIndex][tSide]["relative object"] of pObject) into \
                     pProps["layout"][tRuleIndex][tSide]["relative object"]
            end if
         end if
      end repeat
   end repeat
end lcVCSFilterExportedProps

on lcVCSRegisterUUIDsToResolve pObject,@pUUIDsToResolve
   local tProps
   put the customProperties["mAppProps"] of pObject into tProps
   repeat for each key tRuleIndex in tProps["layout"]
      repeat for each key tObjectIndex in tProps["layout"][tRuleIndex]["objects"]
         put tProps["layout"][tRuleIndex]["objects"][tObjectIndex]["id"]&cr after pUUIDsToResolve
      end repeat
      repeat for each item tSide in "left,top,right,bottom"
         if tProps["layout"][tRuleIndex][tSide]["relative object"] is not empty and \
               tProps["layout"][tRuleIndex][tSide]["relative object"] is not "card" then
            put tProps["layout"][tRuleIndex][tSide]["relative object"]&cr after pUUIDsToResolve
         end if
      end repeat
   end repeat
end lcVCSRegisterUUIDsToResolve

-- translate the row template from a UUID to ID
on lcVCSResolveUUIDs pObject
   local tProps
   put the customProperties["mAppProps"] of pObject into tProps
   repeat for each key tRuleIndex in tProps["layout"]
      repeat for each key tObjectIndex in tProps["layout"][tRuleIndex]["objects"]
         put word 3 of ObjectForUUID(tProps["layout"][tRuleIndex]["objects"][tObjectIndex]["id"]) into \
               tProps["layout"][tRuleIndex]["objects"][tObjectIndex]["id"]
      end repeat
      repeat for each item tSide in "left,top,right,bottom"
         if tProps["layout"][tRuleIndex][tSide]["relative object"] is not empty and \
               tProps["layout"][tRuleIndex][tSide]["relative object"] is not "card" then
            put word 3 of ObjectForUUID(tProps["layout"][tRuleIndex][tSide]["relative object"]) into \
                  tProps["layout"][tRuleIndex][tSide]["relative object"]
         end if
      end repeat
   end repeat
   set the customProperties["mAppProps"] of pObject to tProps
end lcVCSResolveUUIDs
